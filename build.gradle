/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.github.ben-manes.versions' version '0.53.0'
    id 'groovy'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
}

repositories {
    mavenCentral()
    maven { url = 'https://repo.embabel.com/artifactory/libs-snapshot' }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

tasks.withType(GroovyCompile) {
    groovyOptions.parameters = true
}

var embabelVersion = '0.2.0-SNAPSHOT'

// AST transforms should be compiled before the compiler is started, so, as a convenience,
// we put our transform in main and Embabel App in test. Then Gradle will set up the classpath correctly.
// We could alternatively use two subprojects.
dependencies {
    implementation 'org.apache.groovy:groovy:5.0.2'
    implementation "com.fasterxml.jackson.core:jackson-annotations:2.20"

    testImplementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation "com.embabel.agent:embabel-agent-api:$embabelVersion"
    testImplementation "com.embabel.agent:embabel-agent-starter-shell:$embabelVersion"
    testImplementation "com.embabel.agent:embabel-agent-starter-ollama:$embabelVersion"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.14.1'
    testImplementation 'org.junit.platform:junit-platform-launcher:1.14.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.14.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-engine:1.14.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-runner:1.14.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-commons:1.14.1'
}

test {
    useJUnitPlatform()
}

// set up a run task for any Groovy script with "Application" in the name
sourceSets.test.allSource.filter{ it.name.endsWith('.groovy') }.each { source ->
    var name = source.toURI().toString().replaceAll('.*/src/test/groovy/(.*)\\.groovy', '$1')
    if (!name.contains('Application')) return
    name = name.replaceAll('/', '.')
    tasks.register("run.$name", JavaExec) {
        dependsOn compileGroovy
        group = 'Application'
        description = "Run $source.name as a JVM application/Groovy script"
        classpath = sourceSets.test.runtimeClasspath
        mainClass = name
        jvmArgs = ['--enable-native-access=ALL-UNNAMED'] // avoid warning on JDK25
        standardInput = System.in
    }
}